static def isLinuxOrMac() {
    def osName = System.getProperty("os.name").toLowerCase(Locale.ROOT)
    return osName.contains("linux") || osName.contains("mac os") || osName.contains("macos")
}

task copyGitHooks(type: Copy) {
    description = 'Copies the git hooks from ./git-hooks to the .git folder.'
    from("${rootDir}/git-hooks") {
        include '**/*.sh'
        rename '(.*).sh', '$1'
    }
    into "${rootDir}/.git/hooks"
    onlyIf { isLinuxOrMac() }
}

task installGitHooks(type: Exec) {
    description = 'Install the pre-commit git hooks from /git-hooks'
    group = 'git-hooks'
    workingDir = rootDir
    commandLine 'chmod'
    args '-R', '+x', '.git/hooks/'
    dependsOn copyGitHooks
    onlyIf { isLinuxOrMac() }
    doLast {
        logger.lifecycle('âœ… Git hooks installed successfully.')
    }
}
